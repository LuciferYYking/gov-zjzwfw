description = "Poseidon (Bill of Materials)"

apply plugin: 'java-platform'
apply from: "$rootDir/gradle/publications.gradle"

javaPlatform {
    allowDependencies()
}

ext {
    set('mybatisSpringVersion', '2.1.4')
    set('openApiVersion', '1.5.6')
    set('mysqlVersion', '8.0.20')
    set('jaegerCloudVersion', '3.3.1')
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    api platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion")
    api platform("com.alibaba.cloud:spring-cloud-alibaba-dependencies:$springCloudAlibabaVersion")
    constraints {
        api "org.mybatis.spring.boot:mybatis-spring-boot-starter:$mybatisSpringVersion"
        api "io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:$jaegerCloudVersion"
        api "org.springdoc:springdoc-openapi-ui:$openApiVersion"
        api "mysql:mysql-connector-java:$mysqlVersion"
        parent.subprojects.each {
            api it
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "poseidon-dependencies"
            from components.javaPlatform
        }
    }
}